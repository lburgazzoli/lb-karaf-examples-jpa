apply plugin: 'osgi'
apply plugin: 'maven'

version  = '1.0.0.SNAPSHOT'
group    = 'karaf-examples'

project.ext {
    bundleInportPackages = [
            'org.apache.felix.service.command',
            'org.apache.felix.gogo.commands',
            'org.apache.karaf.shell.console',
            '*'
    ]
}

dependencies {
    compile 'org.apache.openjpa:openjpa:2.2.1'
}


task enhance << {
    ant.taskdef(
        name      : 'openjpaenhancer',
        classpath : project.runtimeClasspath.asPath,
        classname : 'org.apache.openjpa.ant.PCEnhancerTask'
    )

    ant.openjpaenhancer(
        classpath : project.runtimeClasspath.asPath)
}

jar {
    it.dependsOn enhance

    baseName = 'jpa-openjpa'

    manifest {
        classesDir = new File("$buildDir/classes/main")
        classpath  = project.runtimeClasspath

        instruction 'Import-Package' , bundleInportPackages.join(',')
        instruction 'Export-Package' , '*'

        attributes(
            'Bundle-Description'     : 'Karaf Examples :: JPA :: OpenJPA',
            'Bundle-SymbolicName'    : project.name,
            'Bundle-Name'            : 'Karaf Examples :: JPA :: OpenJPA',
            'Bundle-Version'         : version,
            'JPA-PersistenceUnits'   : 'DATAPU',
            'Meta-Persistence'       : 'META-INF/persistence.xml'
        )
    }
}




project.ext {
    bundleInportPackages = [
        'org.apache.felix.service.command',
        'org.apache.felix.gogo.commands',
        'org.apache.karaf.shell.console',
        '*'
    ]

    bundleExportPackages = [
        "*"
    ]
}

dependencies {
    compile project(':jpa-commons')
    compile 'org.apache.openjpa:openjpa:2.2.2'
}

task enhance << {
    ant.taskdef(
        name      : 'openjpaenhancer',
        classpath : project.runtimeClasspath.asPath,
        classname : 'org.apache.openjpa.ant.PCEnhancerTask'
    )

    ant.openjpaenhancer(
        classpath : project.runtimeClasspath.asPath)
}

jar {
    it.dependsOn enhance

    baseName = project.name

    manifest {
        classesDir = new File("$buildDir/classes/main")
        classpath  = project.runtimeClasspath

        instruction 'Import-Package' , bundleInportPackages.join(',')
        instruction 'Export-Package' , bundleExportPackages.join(',')

        attributes(
            'Bundle-Description'     : "${project.prefix} :: ${project.name}",
            'Bundle-Name'            : "${project.prefix} :: ${project.name}",
            'Bundle-SymbolicName'    : project.name,
            'Bundle-Version'         : project.version,
            'Implementation-Title'   : "${project.prefix}-${project.name}-${project.version}",
            'Implementation-Version' : project.version,
            'JPA-PersistenceUnits'   : 'DATAPU',
            'Meta-Persistence'       : 'META-INF/persistence.xml'
        )
    }
}



